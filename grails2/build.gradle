buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "org.grails:grails-gradle-plugin:2.2.0.RC1"
    classpath 'org.grails:grails-docs:2.3.6'
  }
}

apply plugin: "grails"
apply from: "gradle/publish.gradle"

repositories {
  grails.central() //creates a maven repo for the Grails Central repository (Core libraries and plugins)
}

grails {
  grailsVersion = '2.4.5'
  groovyVersion = '2.3.9'
  springLoadedVersion '1.2.0.RELEASE'
}

dependencies {
  bootstrap "org.grails.plugins:tomcat:7.0.55" // No container is deployed by default, so add this
  bootstrap "org.grails.plugins:release:3.0.1" // Needed for publishing plugin
  // compile 'org.grails.plugins:asset-pipeline:2.1.3' // Just an example of adding a Grails plugin

  compile project(':commons')
  test 'org.grails.plugins:marshallers:0.6'
}

task gdocs() {
  group 'Documentation'
  description 'Generate Grails Doc style documentation'
  doLast {
    def srcDocs = file("src/docs")
    def outputDir = file("build/docs/manual")
    def publisher = new grails.doc.DocPublisher(srcDocs, outputDir)
    publisher.with {
      ant = project.ant
      title = 'Grails Swaggydoc Plugin'
      subtitle = 'Grails Swaggydoc Plugin'
      version = project.version
      authors = 'Rahul Somasunderam'
      license = 'The Apache Software License, Version 2.0'
      copyright = ''
      footer = ''
      engineProperties = [:]
    }
    publisher.publish()
  }
}
